// Сервис принимающий подписанные документы из MAX /signed_docs_callback
begin(patAgreeID, transactionID, title, fileB64, qrPatAgree, resp ,status, err)

    // {
    //     "patAgreeID" : "482abfd1-f6a5-491a-96a3-08a8bf538487",
    //     "transactionID" : "482abfd1-f6a5-491a-96a3-08a8bf538487",
    //     "templateID" : "482abfd1-f6a5-491a-96a3-08a8bf538487",
    //     "title": "Мой заголовок",
    //     "file": "base64_encoded_string_here",
    //     "signed": true
    // }
    try
    begin()
        // сажаю прилетевшие поля
        @patAgreeID := @Body -> {patAgreeID};
        @transactionID := @Body -> {transactionID};
        @title := @Body -> {title};
        @fileB64 := @Body -> {file};
        
        @resp := new MAX.SIGNED_DOCS_RESP_OUT();
        @status := 200;

        if (RegularExpression("^[0-9a-fA-F]{8}-([0-9a-fA-F]{4}-){3}[0-9a-fA-F]{12}$", @patAgreeID) > 0) 
        begin()

        @qrPatAgree := query top 1 {FileID} from "AKUZ.PATIENT_AGREE"
                        where {FileID}=@patAgreeID;

            if (@qrPatAgree->Count()>0 )  //----- НАЙДЕНЫ ЛИ ЗАПИСИ ------//
            foreach(thisPatAgree in @qrPatAgree)
            begin()
                @thisPatAgree ->{Name} := IsNull(@title,"title");
                @thisPatAgree ->{Content} := FromBase64String(@fileB64);
                @thisPatAgree -> Save();
                // заполняю поля на отдачу
                @resp -> {status} := @thisPatAgree ->{Name};//"success";

            end
            else
            begin()
                @status:=404;
                @resp->{error} := "Не найдены записи по указанному patAgreeID";
            end;

        end else
        begin()
        @status:=400;
            @resp->{error} := "Параметр patAgreeID имеет неверный формат. Идентификатор GUID должен содержать 32 цифры и 4 дефиса (xxxxxxxx-xxxx-xxxx-xxxx-xxxxxxxxxxxx).";
        end;


        begin(rso)
            @rso:=new AKUZ.RABBIT_SEND_OBJECT(new Apartment());
            @rso -> {QueueName}:=@patAgreeID;  
            @rso -> {Content} := Replace(Str(@Body,"Json"),"\"flags_\":\"1\",","");
            @rso -> {Content} := str(@rso -> {Content} + char(13)+char(10)+char(13)+char(10));
            @rso -> {Content} := str(@rso -> {Content} + Replace(Str(@resp,"Json"),"\"flags_\":\"1\",",""));
            @rso -> {RoutingKey}:=@status;
            @rso -> {Type}:="MAX_PAT_AGREE_FILE_IN";
            @rso -> {State}:=1;
            @rso -> {ContentEncoding}:="utf-8";
            @rso -> {CreateDateTime}:= param(CURRENT_DATE_TIME);
            @rso -> Save();
        end;


        @response.StatusCode := @status;
        return(@resp);
    end
    catch(ex)
    begin()
   
        @err := @ex->{InnerException};
        // $throw(@err);

        while (RegExMatch("--> ", @err, "All")->Count()-1>0 )
             @err := SubString(@err,IndexOf(@err,"--> ")+4,StringLen(@err));

        @err := SubString(@err,IndexOf(@err,":")+2,IndexOf(@err,Char(13))-IndexOf(@err,":")-2 );
        // if (str(IsNull(@ex -> {InnerException},""))<>"")
        //     @err:=SubString(str(@ex -> {InnerException}), 0, IndexOf(str(@ex -> {InnerException}),char(13)+char(10)))
        // else
        //     @err:="Прочая ошибка";
 
        @resp := new MAX.SIGNED_DOCS_RESP_OUT();
        @resp -> {error} := @err;
        
        @response.StatusCode := 500;
        return(@resp);
        
    end;    
end
